// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (access/Ownable.sol)

pragma solidity ^0.8.20;

import {Context} from "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * The initial owner is set to the address provided by the deployer. This can
 * later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    /**
     * @dev The caller account is not authorized to perform an operation.
     */
    error OwnableUnauthorizedAccount(address account);

    /**
     * @dev The owner is not a valid owner account. (eg. `address(0)`)
     */
    error OwnableInvalidOwner(address owner);

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.
     */
    constructor(address initialOwner) {
        if (initialOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(initialOwner);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        if (owner() != _msgSender()) {
            revert OwnableUnauthorizedAccount(_msgSender());
        }
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        if (newOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.1.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.20;

import {IERC20} from "./IERC20.sol";
import {IERC20Metadata} from "./extensions/IERC20Metadata.sol";
import {Context} from "../../utils/Context.sol";
import {IERC20Errors} from "../../interfaces/draft-IERC6093.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * The default value of {decimals} is 18. To change this, you should override
 * this function so it returns a different value.
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC-20
 * applications.
 */
abstract contract ERC20 is Context, IERC20, IERC20Metadata, IERC20Errors {
    mapping(address account => uint256) private _balances;

    mapping(address account => mapping(address spender => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the default value returned by this function, unless
     * it's overridden.
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `value`.
     */
    function transfer(address to, uint256 value) public virtual returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, value);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `value` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 value) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, value);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Skips emitting an {Approval} event indicating an allowance update. This is not
     * required by the ERC. See {xref-ERC20-_approve-address-address-uint256-bool-}[_approve].
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `value`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `value`.
     */
    function transferFrom(address from, address to, uint256 value) public virtual returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, value);
        _transfer(from, to, value);
        return true;
    }

    /**
     * @dev Moves a `value` amount of tokens from `from` to `to`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * NOTE: This function is not virtual, {_update} should be overridden instead.
     */
    function _transfer(address from, address to, uint256 value) internal {
        if (from == address(0)) {
            revert ERC20InvalidSender(address(0));
        }
        if (to == address(0)) {
            revert ERC20InvalidReceiver(address(0));
        }
        _update(from, to, value);
    }

    /**
     * @dev Transfers a `value` amount of tokens from `from` to `to`, or alternatively mints (or burns) if `from`
     * (or `to`) is the zero address. All customizations to transfers, mints, and burns should be done by overriding
     * this function.
     *
     * Emits a {Transfer} event.
     */
    function _update(address from, address to, uint256 value) internal virtual {
        if (from == address(0)) {
            // Overflow check required: The rest of the code assumes that totalSupply never overflows
            _totalSupply += value;
        } else {
            uint256 fromBalance = _balances[from];
            if (fromBalance < value) {
                revert ERC20InsufficientBalance(from, fromBalance, value);
            }
            unchecked {
                // Overflow not possible: value <= fromBalance <= totalSupply.
                _balances[from] = fromBalance - value;
            }
        }

        if (to == address(0)) {
            unchecked {
                // Overflow not possible: value <= totalSupply or value <= fromBalance <= totalSupply.
                _totalSupply -= value;
            }
        } else {
            unchecked {
                // Overflow not possible: balance + value is at most totalSupply, which we know fits into a uint256.
                _balances[to] += value;
            }
        }

        emit Transfer(from, to, value);
    }

    /**
     * @dev Creates a `value` amount of tokens and assigns them to `account`, by transferring it from address(0).
     * Relies on the `_update` mechanism
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * NOTE: This function is not virtual, {_update} should be overridden instead.
     */
    function _mint(address account, uint256 value) internal {
        if (account == address(0)) {
            revert ERC20InvalidReceiver(address(0));
        }
        _update(address(0), account, value);
    }

    /**
     * @dev Destroys a `value` amount of tokens from `account`, lowering the total supply.
     * Relies on the `_update` mechanism.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * NOTE: This function is not virtual, {_update} should be overridden instead
     */
    function _burn(address account, uint256 value) internal {
        if (account == address(0)) {
            revert ERC20InvalidSender(address(0));
        }
        _update(account, address(0), value);
    }

    /**
     * @dev Sets `value` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     *
     * Overrides to this logic should be done to the variant with an additional `bool emitEvent` argument.
     */
    function _approve(address owner, address spender, uint256 value) internal {
        _approve(owner, spender, value, true);
    }

    /**
     * @dev Variant of {_approve} with an optional flag to enable or disable the {Approval} event.
     *
     * By default (when calling {_approve}) the flag is set to true. On the other hand, approval changes made by
     * `_spendAllowance` during the `transferFrom` operation set the flag to false. This saves gas by not emitting any
     * `Approval` event during `transferFrom` operations.
     *
     * Anyone who wishes to continue emitting `Approval` events on the`transferFrom` operation can force the flag to
     * true using the following override:
     *
     * ```solidity
     * function _approve(address owner, address spender, uint256 value, bool) internal virtual override {
     *     super._approve(owner, spender, value, true);
     * }
     * ```
     *
     * Requirements are the same as {_approve}.
     */
    function _approve(address owner, address spender, uint256 value, bool emitEvent) internal virtual {
        if (owner == address(0)) {
            revert ERC20InvalidApprover(address(0));
        }
        if (spender == address(0)) {
            revert ERC20InvalidSpender(address(0));
        }
        _allowances[owner][spender] = value;
        if (emitEvent) {
            emit Approval(owner, spender, value);
        }
    }

    /**
     * @dev Updates `owner` s allowance for `spender` based on spent `value`.
     *
     * Does not update the allowance value in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Does not emit an {Approval} event.
     */
    function _spendAllowance(address owner, address spender, uint256 value) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            if (currentAllowance < value) {
                revert ERC20InsufficientAllowance(spender, currentAllowance, value);
            }
            unchecked {
                _approve(owner, spender, currentAllowance - value, false);
            }
        }
    }
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.1) (utils/Context.sol)

pragma solidity ^0.8.20;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    function _contextSuffixLength() internal view virtual returns (uint256) {
        return 0;
    }
}
,// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.6.2;

interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}
,// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.6.2;

import './IUniswapV2Router01.sol';

interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}
,// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// solhint-disable-next-line interface-starts-with-i
interface AggregatorV3Interface {
  function decimals() external view returns (uint8);

  function description() external view returns (string memory);

  function version() external view returns (uint256);

  function getRoundData(
    uint80 _roundId
  ) external view returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound);

  function latestRoundData()
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound);
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.1.0) (interfaces/draft-IERC6093.sol)
pragma solidity ^0.8.20;

/**
 * @dev Standard ERC-20 Errors
 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC-20 tokens.
 */
interface IERC20Errors {
    /**
     * @dev Indicates an error related to the current `balance` of a `sender`. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     * @param balance Current balance for the interacting account.
     * @param needed Minimum amount required to perform a transfer.
     */
    error ERC20InsufficientBalance(address sender, uint256 balance, uint256 needed);

    /**
     * @dev Indicates a failure with the token `sender`. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     */
    error ERC20InvalidSender(address sender);

    /**
     * @dev Indicates a failure with the token `receiver`. Used in transfers.
     * @param receiver Address to which tokens are being transferred.
     */
    error ERC20InvalidReceiver(address receiver);

    /**
     * @dev Indicates a failure with the `spender`’s `allowance`. Used in transfers.
     * @param spender Address that may be allowed to operate on tokens without being their owner.
     * @param allowance Amount of tokens a `spender` is allowed to operate with.
     * @param needed Minimum amount required to perform a transfer.
     */
    error ERC20InsufficientAllowance(address spender, uint256 allowance, uint256 needed);

    /**
     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.
     * @param approver Address initiating an approval operation.
     */
    error ERC20InvalidApprover(address approver);

    /**
     * @dev Indicates a failure with the `spender` to be approved. Used in approvals.
     * @param spender Address that may be allowed to operate on tokens without being their owner.
     */
    error ERC20InvalidSpender(address spender);
}

/**
 * @dev Standard ERC-721 Errors
 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC-721 tokens.
 */
interface IERC721Errors {
    /**
     * @dev Indicates that an address can't be an owner. For example, `address(0)` is a forbidden owner in ERC-20.
     * Used in balance queries.
     * @param owner Address of the current owner of a token.
     */
    error ERC721InvalidOwner(address owner);

    /**
     * @dev Indicates a `tokenId` whose `owner` is the zero address.
     * @param tokenId Identifier number of a token.
     */
    error ERC721NonexistentToken(uint256 tokenId);

    /**
     * @dev Indicates an error related to the ownership over a particular token. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     * @param tokenId Identifier number of a token.
     * @param owner Address of the current owner of a token.
     */
    error ERC721IncorrectOwner(address sender, uint256 tokenId, address owner);

    /**
     * @dev Indicates a failure with the token `sender`. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     */
    error ERC721InvalidSender(address sender);

    /**
     * @dev Indicates a failure with the token `receiver`. Used in transfers.
     * @param receiver Address to which tokens are being transferred.
     */
    error ERC721InvalidReceiver(address receiver);

    /**
     * @dev Indicates a failure with the `operator`’s approval. Used in transfers.
     * @param operator Address that may be allowed to operate on tokens without being their owner.
     * @param tokenId Identifier number of a token.
     */
    error ERC721InsufficientApproval(address operator, uint256 tokenId);

    /**
     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.
     * @param approver Address initiating an approval operation.
     */
    error ERC721InvalidApprover(address approver);

    /**
     * @dev Indicates a failure with the `operator` to be approved. Used in approvals.
     * @param operator Address that may be allowed to operate on tokens without being their owner.
     */
    error ERC721InvalidOperator(address operator);
}

/**
 * @dev Standard ERC-1155 Errors
 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC-1155 tokens.
 */
interface IERC1155Errors {
    /**
     * @dev Indicates an error related to the current `balance` of a `sender`. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     * @param balance Current balance for the interacting account.
     * @param needed Minimum amount required to perform a transfer.
     * @param tokenId Identifier number of a token.
     */
    error ERC1155InsufficientBalance(address sender, uint256 balance, uint256 needed, uint256 tokenId);

    /**
     * @dev Indicates a failure with the token `sender`. Used in transfers.
     * @param sender Address whose tokens are being transferred.
     */
    error ERC1155InvalidSender(address sender);

    /**
     * @dev Indicates a failure with the token `receiver`. Used in transfers.
     * @param receiver Address to which tokens are being transferred.
     */
    error ERC1155InvalidReceiver(address receiver);

    /**
     * @dev Indicates a failure with the `operator`’s approval. Used in transfers.
     * @param operator Address that may be allowed to operate on tokens without being their owner.
     * @param owner Address of the current owner of a token.
     */
    error ERC1155MissingApprovalForAll(address operator, address owner);

    /**
     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.
     * @param approver Address initiating an approval operation.
     */
    error ERC1155InvalidApprover(address approver);

    /**
     * @dev Indicates a failure with the `operator` to be approved. Used in approvals.
     * @param operator Address that may be allowed to operate on tokens without being their owner.
     */
    error ERC1155InvalidOperator(address operator);

    /**
     * @dev Indicates an array length mismatch between ids and values in a safeBatchTransferFrom operation.
     * Used in batch transfers.
     * @param idsLength Length of the array of token identifiers
     * @param valuesLength Length of the array of token amounts
     */
    error ERC1155InvalidArrayLength(uint256 idsLength, uint256 valuesLength);
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.1.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC-20 standard as defined in the ERC.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the value of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the value of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves a `value` amount of tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 value) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the
     * caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 value) external returns (bool);

    /**
     * @dev Moves a `value` amount of tokens from `from` to `to` using the
     * allowance mechanism. `value` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 value) external returns (bool);
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.1.0) (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.20;

import {IERC20} from "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC-20 standard.
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/Pausable.sol)

pragma solidity ^0.8.20;

import {Context} from "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    bool private _paused;

    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    /**
     * @dev The operation failed because the contract is paused.
     */
    error EnforcedPause();

    /**
     * @dev The operation failed because the contract is not paused.
     */
    error ExpectedPause();

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        if (paused()) {
            revert EnforcedPause();
        }
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        if (!paused()) {
            revert ExpectedPause();
        }
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}
,// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.1.0) (utils/ReentrancyGuard.sol)

pragma solidity ^0.8.20;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If EIP-1153 (transient storage) is available on the chain you're deploying at,
 * consider using {ReentrancyGuardTransient} instead.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant NOT_ENTERED = 1;
    uint256 private constant ENTERED = 2;

    uint256 private _status;

    /**
     * @dev Unauthorized reentrant call.
     */
    error ReentrancyGuardReentrantCall();

    constructor() {
        _status = NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be NOT_ENTERED
        if (_status == ENTERED) {
            revert ReentrancyGuardReentrantCall();
        }

        // Any calls to nonReentrant after this point will fail
        _status = ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == ENTERED;
    }
}
,// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/utils/Pausable.sol";
import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import {AggregatorV3Interface} from "@chainlink/contracts/src/v0.8/shared/interfaces/AggregatorV3Interface.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

import "@uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";

interface ISwapPlatform {
    function WETH() external view returns (address);

    function balanceOf(address account) external view returns (uint256);

    function swapExactTokensForETH(
        uint256 tokenAmount,
        uint256 minEthAmount,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);
}

/**
 * @title KhacToken
 * @dev ERC20 token with liquidity management, market making, price updates, and platform validation.
 */
contract KhacToken is ERC20, Pausable, ReentrancyGuard, Ownable {
    uint256 private constant TOTAL_SUPPLY_CAP = 10_000_000 * 10**18;
    uint256 private constant PRICE_FLOOR = 550 * 1e18; // in USD // in USD
    uint256 public dailyTradeVolume = 100 * 10**18; // Initial daily trade volume
    uint256 public smallTransactionFee = 10; // 10% for small transactions
    uint256 public largeTransactionFee = 5; // 5% for large transactions
    uint256 public lastTradeUpdate;
    uint256 public lastPriceUpdate;
    address private self = address(this); // Cache address(this)
    uint256 public  tokenBalance = balanceOf(self);
    uint256 public  ethBalance = self.balance; 
    uint256 public tokenPrice;
    uint256 public minimumLiquidity;
    IUniswapV2Router02 public uniswapRouter;
    AggregatorV3Interface public priceFeed;

    mapping(address => bool) public validatedPlatforms;
    uint256 public lastValidationTime;

    event InitialPriceSet(uint256 price);
    event PriceUpdated(uint256 newPrice);
    event DailyTradeExecuted(uint256 volume, uint256 ethReceived);
    event LiquidityAdded(uint256 tokenAmount, uint256 ethAmount);
    event TokensSwapped(
        address indexed user,
        uint256 tokenAmount,
        uint256 ethReceived
    );
    event Withdrawal(address indexed to, uint256 amount);
    event MinimumLiquidityUpdated(uint256 newMinimum);
    event PlatformValidated(address platform);
    event MarketMakingExecuted(
        address platform,
        uint256 tokensSwapped,
        uint256 ethReceived
    );
     event TransactionDetails(address indexed user, uint256 amount, string action);

    /**
     * @dev Constructor initializes the contract with Uniswap router, Chainlink price feed, and total supply.
     * @param _priceFeed Address of the Chainlink price feed.
     * @param _uniswapRouter Address of the Uniswap V2 router.
     */
    constructor(address _priceFeed, address _uniswapRouter)
        ERC20("KharYsma Coins", "KHACN")
        payable 
        Ownable(msg.sender)
    {
        uniswapRouter = IUniswapV2Router02(_uniswapRouter);
        priceFeed = AggregatorV3Interface(_priceFeed);
        _mint(msg.sender, TOTAL_SUPPLY_CAP);
        emit InitialPriceSet(tokenPrice);
    }
    

     /**
     * @dev Allows the owner to pause the contract.
     */
    function pauseContract() external payable onlyOwner {
        _pause();
    }

    /**
     * @dev Allows the owner to unpause the contract.
     */
    function unpauseContract() external payable onlyOwner {
        _unpause();
    }
     /**
     * @dev Distribute transaction fees dynamically.
     * @param transactionAmount Amount of the transaction.
     * @param sender Address initiating the transaction.
     */
    function calculateAndDistributeFees(
        uint256 transactionAmount,
        address sender
    ) internal {
        
        uint256 feePercentage = transactionAmount < 10_000 * 1e18
            ? smallTransactionFee
            : largeTransactionFee;
        uint256 fee = (transactionAmount * feePercentage) / 100;
        uint256 ownerShare = (fee * 60) / 100;
        uint256 liquidityShare = fee - ownerShare;

        _transfer(sender, owner(), ownerShare);
        _transfer(sender, self, liquidityShare);
        emit TransactionDetails(sender, fee, "Fee Distributed");
    }
    /**
     * @dev Updates the token price using the Chainlink price feed.
     */
    function updatePrice() public {
        (, int256 price, , , ) = priceFeed.latestRoundData();
        require(price != 0, "Invalid price from oracle");
        tokenPrice = uint256(price);
        emit PriceUpdated(tokenPrice);
    }
     /**
     * @dev Enforces the price floor before transactions.
     * @param khacnAmount Amount of KHACN tokens.
     */
    // Modifier for enforcing price floor before transactions.
    modifier enforcePriceFloor (uint256 khacnAmount)  {
            require(isAbovePriceFloor(khacnAmount), "Transaction below price floor");
            _;
    }
      /**
     * @dev Checks if the transaction is above the price floor.
     * @param khacnAmount Amount of KHACN tokens.
     */
    function isAbovePriceFloor(uint256 khacnAmount) public view returns (bool) {
        (, int256 price, , , ) = priceFeed.latestRoundData();
        uint256 khacnValue = uint256(price) * khacnAmount / 1e8;
        return khacnValue >= PRICE_FLOOR;
    }

        /**
     * @dev Updates daily trade volume (5% growth per day).
     */
    function updateDailyTradeVolume() internal {
        require(block.timestamp > lastTradeUpdate + 1 days, "Volume update not due");
        dailyTradeVolume = (dailyTradeVolume * 105) / 100; // 5% increase
        lastTradeUpdate = block.timestamp;
    }

    /**
     * @dev Updates token price (2% growth per day).
     */
    function increaseTokenPrice() internal {
        require(block.timestamp > lastPriceUpdate + 1 days, "Price update not due");
        tokenPrice = (tokenPrice * 102) / 100; // 2% increase
        lastPriceUpdate = block.timestamp;
        emit PriceUpdated(tokenPrice);
    }

    /**
     * @dev Performs a daily trade using the updated daily trade volume.
     */
    function performDailyTrade() internal onlyOwner enforcePriceFloor(balanceOf(self)) {
        updateDailyTradeVolume();
        //address loSelf = self; // Cache address(this)
        //uint256 tokenBalance = balanceOf(self);
        require(tokenBalance > dailyTradeVolume, "Insufficient tokens for trade");

        _approve(self, address(uniswapRouter), dailyTradeVolume);

        address[] memory path = new address[](2);
        path[0] = self;
        path[1] = uniswapRouter.WETH();

        uniswapRouter.swapExactTokensForETH(
            dailyTradeVolume,
            0,
            path,
            self,
            block.timestamp
        );

        emit DailyTradeExecuted(dailyTradeVolume, ethBalance);
    }

    /**
     * @dev Updates price and performs daily trade.
     */
    function updatePriceAndPerformTrade() external payable  onlyOwner {
        increaseTokenPrice();
        performDailyTrade();
    }

    /**
     * @dev Automates daily operations (trade volume and price updates).
     */
    function automateDailyOperations() external payable onlyOwner {
        updateDailyTradeVolume();
        increaseTokenPrice();
        performDailyTrade();
    }

    /**
     * @dev Injects accumulated liquidity into Uniswap pool.
     */
    function injectLiquidityAutomatically() external payable onlyOwner enforcePriceFloor(balanceOf(address(this)))  {
        //address loSelf=self;
        //uint256 tokenBalance = balanceOf(self);
        //uint256 ethBalance = self.balance; //selfbalance(this)//address(this).balance;

        require(tokenBalance != 0, "Insufficient token balance");
        require(ethBalance != 0, "Insufficient ETH balance");

        _approve(self, address(uniswapRouter), tokenBalance);

        uniswapRouter.addLiquidityETH{value: ethBalance}(
            self,
            tokenBalance,
            0, // Minimum tokens
            0, // Minimum ETH
            owner(),
            block.timestamp
        );
        emit TransactionDetails(msg.sender, tokenBalance, "Liquidity Injected");
    }

   

    /**
     * @dev Allows the owner to mint new tokens up to the supply cap.
     * @param to Recipient address.
     * @param amount Amount of tokens to mint.
     */
    function mint(address to, uint256 amount) external payable onlyOwner {
        require(
            totalSupply() + amount < TOTAL_SUPPLY_CAP,
            "Exceeds total supply cap"
        );
        _mint(to, amount);
    }

    /**
     * @dev Updates the minimum liquidity required for market making.
     * @param newMinimum New minimum liquidity value.
     */
    function setMinimumLiquidityForMarketMaking(uint256 newMinimum)
        external
        payable
        onlyOwner
    {
        require(minimumLiquidity != newMinimum, "New value must be different");
        minimumLiquidity = newMinimum;
        emit MinimumLiquidityUpdated(newMinimum);
    }

    /**
     * @dev Swaps tokens for ETH using Uniswap.
     * @param tokenAmount Amount of tokens to swap.
     */
    function swapTokens(uint256 tokenAmount) external payable onlyOwner enforcePriceFloor(balanceOf(self))  {
       //address loSelf=self;
        _approve(self, address(uniswapRouter), tokenAmount);
        // Calculate and distribute fees
        calculateAndDistributeFees(tokenAmount, msg.sender);
        address[] memory path = new address[](2);
        path[0] = self;
        path[1] = uniswapRouter.WETH();

        uint256 initialEthBalance = ethBalance;

        uniswapRouter.swapExactTokensForETH(
            tokenAmount,
            0, // Accept any amount of ETH
            path,
            self,
            block.timestamp
        );

        uint256 ethReceived = ethBalance - initialEthBalance;

        emit TokensSwapped(msg.sender, tokenAmount, ethReceived);
    }

    /**
     * @dev Validates a swap platform by checking WETH support.
     * @param platform Address of the platform to validate.
     */
    function autoValidatePlatform(address platform) public   onlyOwner {
        address  platformAddress=address(0);
        require(platform != platformAddress ,"Invalid platform address");

        try ISwapPlatform(platform).WETH() returns (address wethAddress) {
            if (wethAddress != platformAddress) {
                validatedPlatforms[platform] = true;
                emit PlatformValidated(platform);
            }
        } catch {
            validatedPlatforms[platform] = false;
        }
    }

    /**
     * @dev Executes market making on validated platforms.
     * @param tokenAmount Amount of tokens to swap.
     */
    function executeMarketMaking(uint256 tokenAmount) public onlyOwner enforcePriceFloor(balanceOf(self)) {
        require(tokenAmount != 0, "Invalid token amount");
        //address loSelf =self;
        address[] memory validatedPlatformList = getValidatedPlatforms();
        uint256 platformLength = validatedPlatformList.length;

        // Calculate and distribute fees before executing market-making
        calculateAndDistributeFees(tokenAmount, msg.sender);
        for (uint256 i = 0; i < platformLength;) {
            address platform = validatedPlatformList[i];
            address[] memory path = new address[](2);
            path[0] = self;
            path[1] = ISwapPlatform(platform).WETH();

            try
                ISwapPlatform(platform).swapExactTokensForETH(
                    tokenAmount,
                    0,
                    path,
                    self,
                    block.timestamp
                )
            returns (uint256[] memory amounts) {
                emit MarketMakingExecuted(platform, tokenAmount, amounts[1]);
            } catch {
                continue;
            }
            unchecked {
                ++i;
            }
        }
    }

    /**
     * @dev Automatically validates platforms every 7 days.
     */
    function validatePlatformsAutomatically() public onlyOwner {
        require(
            block.timestamp > lastValidationTime + 7 days,
            "Validation not yet due"
        );
        lastValidationTime = block.timestamp;
        address[] memory potentialPlatforms = getPotentialPlatforms();
        uint256 platformLength = potentialPlatforms.length;
        for (uint256 i = 0; i < platformLength;) {
            autoValidatePlatform(potentialPlatforms[i]);
            unchecked {
                ++i;
            }
            
        }
    }

    /**
     * @dev Returns a list of validated platforms.
     */
    function getValidatedPlatforms() public view returns (address[] memory platforms) {
        address[] memory potential = getPotentialPlatforms();
        uint256 potentialLength = potential.length;
        uint256 count = 0;

        for (uint256 i = 0; i < potentialLength;) {
            address potenyial_index =potential[i];
            bool validedPlatformPotential=validatedPlatforms[potenyial_index];
           
            unchecked {
                 if (validedPlatformPotential) count++;
                ++i;
            }
        }

        platforms = new address[](count);
        uint256 index = 0;

        for (uint256 i = 0; i < potentialLength;) {
            address potenyialValidatedindex =potential[i];
            bool validedPlatformPotentialIndex=validatedPlatforms[potenyialValidatedindex];
            
            unchecked {
                if (validedPlatformPotentialIndex) {
                    platforms[index] = potenyialValidatedindex;
                    ++index;
                }
                ++i;
            }
        }

    
    }

    /**
     * @dev Placeholder for retrieving potential platforms.
     */
    function getPotentialPlatforms() public pure returns (address[] memory) {
        return new address[](0); //Placeholder for dynamic population
    }

    /**
     * @dev Withdraws ETH from the contract.
     * @param to Recipient address.
     * @param amount Amount of ETH to withdraw.
     */
    function withdrawETH(address to, uint256 amount)
        external
        payable
        onlyOwner
        nonReentrant
    {
        
        require(self.balance > amount, "Insufficient balance");
        payable(to).transfer(amount);
        emit Withdrawal(to, amount);
    }

    /**
     * @dev Locks 30% of the total supply for liquidity reserve.
     */
    function lockLiquidityReserve() external payable onlyOwner {
        uint256 reserveAmount = (TOTAL_SUPPLY_CAP * 30) / 100;
        
        _transfer(msg.sender, self, reserveAmount);

        emit TransactionDetails(msg.sender, reserveAmount, "Liquidity Reserve Locked");
    }

    /**
     * @dev Adjusts fees dynamically based on market conditions using Chainlink Oracle.
     */
    function adjustFeesBasedOnMarket() external payable  onlyOwner {
        (, int256 ethPrice, , , ) = priceFeed.latestRoundData();
        uint256 cryptoIndex = uint256(ethPrice);
        smallTransactionFee = cryptoIndex > 1000 ? 7 : 10;
        largeTransactionFee = cryptoIndex > 1000 ? 3 : 5;

        emit TransactionDetails(msg.sender, 0, "Fees Adjusted");
    }


    /**
     * @dev Allows the contract to accept ETH payments.
     */
    receive() external payable {
        emit LiquidityAdded(0, msg.value);
    }

    /**
     * @dev Sets the allowance for a spender safely to prevent front-running attacks.
     * This method requires the current allowance to be reset to zero before assigning a new value.
     * @param spender Address of the spender.
     * @param amount Amount of tokens to approve.
     */
    function safeApprove(address spender, uint256 amount) external {
        require(
            allowance(msg.sender, spender) == 0 || amount == 0,
            "Allowance must be zero"
        );
        _approve(msg.sender, spender, amount);
    }

    /**
     * @dev Safely increases the allowance for a spender.
     * @param spender Address of the spender.
     * @param addedValue Amount to increase the allowance by.
     */
    function increaseAllowance(address spender, uint256 addedValue) external returns (bool) {
        _approve(msg.sender, spender, allowance(msg.sender, spender) + addedValue);
        return true;
    }

    /**
     * @dev Safely decreases the allowance for a spender.
     * @param spender Address of the spender.
     * @param subtractedValue Amount to decrease the allowance by.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) external returns (bool) {
        uint256 currentAllowance = allowance(msg.sender, spender);
        require(currentAllowance > subtractedValue, "Decreased allowance below zero");
        _approve(msg.sender, spender, currentAllowance - subtractedValue);
        return true;
    }
}
